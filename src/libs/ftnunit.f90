
!========================================================================
!
!                          T o m o f a s t - x
!                        -----------------------
!
!           Authors: Vitaliy Ogarko, Jeremie Giraud, Roland Martin.
!
!               (c) 2021 The University of Western Australia.
!
! The full text of the license is available in file "LICENSE".
!
!========================================================================

! ftnunit.f90 --
!     Module that implements part of the "ftnunit" framework:
!     It is inspired by the well-known JUnit framework for
!     integrating unit tests in a Java application.
!
!     The module offers:
!     - a set of common utilities, such as assertion checking
!       routines
!     - a general routine to run the tests if requested
!     - resources that keep track of the status
!
!     Related files:
!     ftnunit_test.f90 -- deprecated
!     runtests.bat
!     runtests.sh
!     runtests.tcl
!
!     $Id: ftnunit.f90,v 1.3 2008/01/27 09:08:31 arjenmarkus Exp $
!
!     Vitaliy Ogarko: Adjusted to support parallel tests (MPI).
!
module ftnunit
    use global_typedefs

    implicit none

    integer, private, save :: last_test           ! Last test that was started
    integer, private, save :: testno              ! Current test number
    integer, private, save :: nofails             ! Number of assertions that failed
    integer, private, save :: noruns              ! Number of runs so far
    logical, private, save :: call_final = .true. ! Call runtests_final implicitly?

    interface assert_equal
        module procedure assert_equal_int
        module procedure assert_equal_int1d
    end interface

    interface assert_comparable
        module procedure assert_comparable_real
        module procedure assert_comparable_real1d
    end interface

contains


! test --
!     Routine to run a unit test
! Arguments:
!     proc          The subroutine implementing the unit test
!     text          Text describing the test
!
subroutine test( proc, text, myrank, nbproc )
    ! VO: Added interface.
    interface
        subroutine proc(myrank, nbproc)
            integer, intent(in) :: myrank, nbproc
        end subroutine proc
    end interface

    external            :: proc
    character(len=*)    :: text
    integer, intent(in) :: myrank, nbproc

    integer           :: lun

    !
    ! Check if the test should run
    !
    testno = testno + 1
    if ( testno <= last_test ) then
        print *, 'Skipping test number =', testno, ', last test =', last_test, ', myrank =', myrank
        return
    endif

    if (myrank == 0) then
      !
      ! Record the fact that we started the test
      !
      call ftnunit_get_lun( lun )
      open( lun, file = 'ftnunit.lst' )
      write( lun, * ) testno, nofails, noruns
      close( lun )

      !
      ! Run the test
      !
      write( *, '(2a)' ) 'Test: ', trim(text)
    endif

    call proc(myrank, nbproc)

    if (myrank == 0) then
      !
      ! No runtime error or premature end of
      ! the program ...
      !
      call ftnunit_get_lun( lun )
      open( lun, file = 'ftnunit.lst' )
      write( lun, * ) testno, nofails, noruns
      close( lun )
    endif

end subroutine test

! runtests_init --
!     Subroutine to initialise the ftnunit system
! Arguments:
!     None
! Note:
!     Use in conjunction with runtests_final to enable multiple calls
!     to the runtests subroutine. This makes it easier to run tests
!     from different modules, as you have more than one subroutine to
!     do the actual tests.
!
subroutine runtests_init
    call_final = .false.
end subroutine

! runtests_final --
!     Subroutine to report the overall statistics
! Arguments:
!     None
! Note:
!     Use in conjunction with runtests_init to enable multiple calls
!     to the runtests subroutine. This makes it easier to run tests
!     from different modules, as you have more than one subroutine to
!     do the actual tests.
!
subroutine runtests_final(myrank)
  integer, intent(in) :: myrank
  integer :: exist_loc, exist_glob, ierr
  integer :: nofails_glob

  exist_loc = 0

  if ( ftnunit_file_exists("ftnunit.run") ) then
    exist_loc = 1
  endif

  call mpi_allreduce(exist_loc, exist_glob, 1, MPI_INTEGER, MPI_SUM, MPI_COMM_WORLD, ierr)

  ! Gather the total number of fails from all CPUs.
  call mpi_allreduce(nofails, nofails_glob, 1, MPI_INTEGER, MPI_SUM, MPI_COMM_WORLD, ierr)
  nofails = nofails_glob

  if (exist_glob > 0) then
  ! At least one CPU found the "ftnunit.run" file (before it gets removed).

    if (myrank == 0) then
      print *, '******************************************************************'
      write(*,'(a,i5)') 'Number of failed assertions:                ', nofails
      write(*,'(a,i5)') 'Number of runs needed to complete the tests:', noruns
      write(*,'(a,i5)') 'Total number of tests were run:             ', testno
      print *, '******************************************************************'

      call ftnunit_remove_file( "ftnunit.lst" )
      call ftnunit_remove_file( "ftnunit.run" )
    endif

    call MPI_Finalize(ierr)
    stop
  endif

end subroutine

! runtests --
!     Subroutine to run the tests if requested
! Arguments:
!     testproc      The test subroutine that actually runs the unit test
!
subroutine runtests( testproc, myrank, nbproc )
  interface
    subroutine testproc(myrank, nbproc)
      integer, intent(in) :: myrank, nbproc
    end subroutine testproc
  end interface

  integer, intent(in) :: myrank, nbproc

  integer :: lun
  integer :: ierr

  last_test = 0
  nofails   = 0
  noruns    = 0
  testno    = 0

  if ( ftnunit_file_exists("ftnunit.run") ) then

    if (myrank == 0) then
      if ( ftnunit_file_exists("ftnunit.lst") ) then

        call ftnunit_get_lun( lun )
        open( lun, file = "ftnunit.lst", iostat = ierr )

        if ( ierr == 0 ) then
          read( lun, *, iostat = ierr ) last_test, nofails, noruns

          if ( ierr /= 0 ) then
              last_test = 0
              nofails   = 0
              noruns    = 0
          endif
          close( lun )
        endif

      endif
    endif

    call MPI_Bcast(last_test, 1, MPI_INTEGER, 0, MPI_COMM_WORLD, ierr)
    call MPI_Bcast(nofails, 1, MPI_INTEGER, 0, MPI_COMM_WORLD, ierr)
    call MPI_Bcast(noruns, 1, MPI_INTEGER, 0, MPI_COMM_WORLD, ierr)

    noruns = noruns + 1

    call testproc(myrank, nbproc)

    if ( call_final ) then
      call runtests_final(myrank)
    endif

  endif

end subroutine runtests

! assert_true --
!     Subroutine to check if a condition is true
! Arguments:
!     cond          Condition to be checked
!     text          Text describing the assertion
! Side effects:
!     If the assertion fails, this is reported to standard
!     output. Also, nofails is increased by one.
!
subroutine assert_true( cond, text )
    logical, intent(in)          :: cond
    character(len=*), intent(in) :: text

    if ( .not. cond ) then
        nofails = nofails + 1
        write(*,*) '    Condition "',trim(text), '" failed'
        write(*,*) '    It should have been true'
    endif
end subroutine assert_true

! assert_false --
!     Subroutine to check if a condition is false
! Arguments:
!     cond          Condition to be checked
!     text          Text describing the assertion
! Side effects:
!     If the assertion fails, this is reported to standard
!     output. Also, nofails is increased by one.
!
subroutine assert_false( cond, text )
    logical, intent(in)          :: cond
    character(len=*), intent(in) :: text

    if ( cond ) then
        nofails = nofails + 1
        write(*,*) '    Condition "',trim(text), '" failed'
        write(*,*) '    It should have been false'
    endif
end subroutine assert_false

! assert_equal_int --
!     Subroutine to check if two integers are equal
! Arguments:
!     value1        First value
!     value2        Second value
!     text          Text describing the assertion
! Side effects:
!     If the assertion fails, this is reported to standard
!     output. Also, nofails is increased by one.
!
subroutine assert_equal_int( value1, value2, text )
    integer, intent(in)          :: value1
    integer, intent(in)          :: value2
    character(len=*), intent(in) :: text

    if ( value1 /= value2) then
        nofails = nofails + 1
        write(*,*) '    Values not equal: "',trim(text), '" - assertion failed'
        write(*,*) '    Values: ', value1, ' and ', value2
    endif
end subroutine assert_equal_int

! assert_equal_int1d --
!     Subroutine to check if two integer arrays are equal
! Arguments:
!     array1        First array
!     array2        Second array
!     text          Text describing the assertion
! Side effects:
!     If the assertion fails, this is reported to standard
!     output. Also, nofails is increased by one.
!
subroutine assert_equal_int1d( array1, array2, text )
    integer, dimension(:), intent(in) :: array1
    integer, dimension(:), intent(in) :: array2
    character(len=*), intent(in)      :: text

    integer                           :: i
    integer                           :: count

    if ( size(array1) /= size(array2) ) then
        nofails = nofails + 1
        write(*,*) '    Arrays have different sizes: "',trim(text), '" - assertion failed'
    else
        if ( any( array1 /= array2 ) ) then
            nofails = nofails + 1
            write(*,*) '    One or more values different: "',trim(text), '" - assertion failed'
            count = 0
            do i = 1,size(array1)
                if ( array1(i) /= array2(i) ) then
                    count = count + 1
                    write(*,'(3a10)')    '    Index', '     First', '    Second'
                    if ( count < 50 ) then
                        write(*,'(3i10)')    i, array1(i), array2(i)
                    endif
                    write(*,*) 'Number of differences: ', count
                endif
            enddo
        endif
    endif
end subroutine assert_equal_int1d

! assert_comparable_real --
!     Subroutine to check if two reals are approximately equal
! Arguments:
!     value1        First value
!     value2        Second value
!     margin        Allowed margin (relative)
!     text          Text describing the assertion
! Side effects:
!     If the assertion fails, this is reported to standard
!     output. Also, nofails is increased by one.
!
subroutine assert_comparable_real( value1, value2, margin, text )
    real(kind=CUSTOM_REAL), intent(in)             :: value1
    real(kind=CUSTOM_REAL), intent(in)             :: value2
    real(kind=CUSTOM_REAL), intent(in)             :: margin
    character(len=*), intent(in) :: text

    if ( abs(value1-value2) > 0.5 * margin * (abs(value1)+abs(value2)) ) then
        nofails = nofails + 1
        write(*,*) '    Values not comparable: "',trim(text), '" - assertion failed'
        write(*,*) '    Values: ', value1, ' and ', value2
    endif
end subroutine assert_comparable_real

! assert_compatable_real1d --
!     Subroutine to check if two real arrays are comparable
! Arguments:
!     array1        First array
!     array2        Second array
!     text          Text describing the assertion
! Side effects:
!     If the assertion fails, this is reported to standard
!     output. Also, nofails is increased by one.
!
subroutine assert_comparable_real1d( array1, array2, margin, text )
    real(kind=CUSTOM_REAL), dimension(:), intent(in)    :: array1
    real(kind=CUSTOM_REAL), dimension(:), intent(in)    :: array2
    real(kind=CUSTOM_REAL), intent(in)                  :: margin
    character(len=*), intent(in)      :: text

    integer                           :: i
    integer                           :: count

    if ( size(array1) /= size(array2) ) then
        nofails = nofails + 1
        write(*,*) '    Arrays have different sizes: "',trim(text), '" - assertion failed'
    else
        if ( any( abs(array1-array2) > 0.5 * margin * (abs(array1)+abs(array2)) ) ) then
            nofails = nofails + 1
            write(*,*) '    One or more values different: "',trim(text), '" - assertion failed'
            count = 0
            do i = 1,size(array1)
                if ( abs(array1(i)-array2(i)) > &
                         0.5 * margin * (abs(array1(i))+abs(array2(i))) ) then
                    count = count + 1
                    write(*,'(a10,2a15)')    '    Index', '          First', '         Second'
                    if ( count < 50 ) then
                        write(*,'(i10,e15.5)')    i, array1(i), array2(i)
                    endif
                    write(*,*) 'Number of differences: ', count
                endif
            enddo
        endif
    endif
end subroutine assert_comparable_real1d

! ftnunit_file_exists --
!     Auxiliary function to see if a file exists
! Arguments:
!     filename      Name of the file to check
! Returns:
!     .true. if the file exists, .false. otherwise
!
logical function ftnunit_file_exists( filename )
    character(len=*), intent(in) :: filename

    inquire( file = filename, exist = ftnunit_file_exists )
end function ftnunit_file_exists

! ftnunit_get_lun --
!     Auxiliary subroutine to get a free LU-number
! Arguments:
!     lun           The value that can be used
!
subroutine ftnunit_get_lun( lun )
    integer, intent(out) :: lun

    logical       :: opend
    integer, save :: prevlun = 0

    if ( prevlun /= 0 ) then
        inquire( unit = lun, opened = opend )
        if ( .not. opend ) then
            lun = prevlun
            return
        endif
    endif

    do prevlun = 10,99
        inquire( unit = prevlun, opened = opend )
        if ( .not. opend ) then
            lun = prevlun
            return
        endif
    enddo

end subroutine ftnunit_get_lun

! ftnunit_remove_file --
!     Auxiliary subroutine to remove a file
! Arguments:
!     filename      Name of the file to be removed
!
subroutine ftnunit_remove_file( filename )
    character(len=*), intent(in) :: filename

    integer                      :: lun
    integer                      :: ierr

    call ftnunit_get_lun( lun )
    open( lun, file = filename, iostat = ierr )
    if ( ierr /= 0 ) then
        write(*,*) '    Could not open file for removal: ', trim(filename)
        nofails = nofails + 1
    else
        close( lun, status = 'delete' )
        if ( ftnunit_file_exists( filename ) ) then
            write(*,*) '    Removal of file unsuccssful: ', trim(filename)
            nofails = nofails + 1
        endif
    endif

end subroutine ftnunit_remove_file

! ftnunit_make_empty_file --
!     Auxiliary subroutine to make an empty file
! Arguments:
!     filename      Name of the file to be created
!
subroutine ftnunit_make_empty_file( filename )
    character(len=*), intent(in) :: filename

    integer                      :: lun
    integer                      :: ierr

    if ( ftnunit_file_exists( filename ) ) then
        call ftnunit_remove_file( filename )
    endif
    call ftnunit_get_lun( lun )
    open( lun, file = filename, iostat = ierr, status = 'new' )
    if ( ierr /= 0 ) then
        write(*,*) '    Failed to create empty file: ', trim(filename)
        nofails = nofails + 1
    else
        close( lun )
    endif

end subroutine ftnunit_make_empty_file

end module ftnunit
